class StarHackGame {

    static int timeLeft;
    static int displayMode;
    static PlayerShip playerShip;
    static ObjectArray anomalies;
    static int anomaliesLeft;
    static ObjectArray habitats;
    static int habitatsLeft;
    static boolean friendlyFire;
    static ObjectArray stars;
    static ObjectArray sectorScans;

    function void init() {
        let playerShip = Array.new(10);
        let sectorScans = ObjectArray.new(64, 4);
        let stars = ObjectArray.new(32, 2);
        let habitats = ObjectArray.new(habitatsLeft, 5);
        let anomalies = ObjectArray.new(anomaliesLeft, 4);
        return;
    }

    function void reset() {
        register int i, sectorIndex;
        register Anomaly anomaly;
        register Habitat habitat;
        register Star star;
        register SectorScan sectorScan;

        let timeLeft = 1000;
        let friendlyFire = 0;
        let displayMode = 0;

        do TextGraphics.drawTextAligned(Constants.msg00(), 2, 3, 0);

        do sectorScans.clear();

        let sectorIndex = StarHackGame.randomSector();
        do playerShip.init(sectorIndex, StarHackGame.randomSubsector(sectorIndex));

        do TextGraphics.drawTextAligned(Constants.msg01(), 4, 6, 0);

        let i = 0;
        while (i < 32) {
            let star = stars.get(i);
            let sectorIndex = StarHackGame.randomSector();
            do star.init(sectorIndex, StarHackGame.randomSubsector(sectorIndex));
            let sectorScan = sectorScans.get(sectorIndex);
            do sectorScan.incStars(1);
            inc i;
        }

        do TextGraphics.drawTextAligned(Constants.msg02(), 5, 6, 0);

        let habitatsLeft = 8;
        let i = 0;
        while (i < habitatsLeft) {
            let habitat = habitats.get(i);
            let sectorIndex = StarHackGame.randomSector();
            do habitat.init(sectorIndex, StarHackGame.randomSubsector(sectorIndex), 50 + Random.randInt(100), Random.randInt(2), 10000);
            let sectorScan = sectorScans.get(sectorIndex);
            do sectorScan.incHabitats(1);
            inc i;
        }

        do TextGraphics.drawTextAligned(Constants.msg03(), 7, 6, 0);

        let anomaliesLeft = 8;
        let i = 0;
        while (i < anomaliesLeft) {
            let anomaly = anomalies.get(i);
            let sectorIndex = StarHackGame.randomSector();
            do anomaly.init(sectorIndex, StarHackGame.randomSubsector(sectorIndex), 100);
            let sectorScan = sectorScans.get(sectorIndex);
            do sectorScan.incAnomalies(1);
            inc i;
        }

        do TextGraphics.drawLabelValue(Constants.daysLeft(), (timeLeft / 100), 10, 32, 0);

        return;
    }

    function int randomSector() {
        return Random.randInt(64);
    }

    function int randomSubsector(int sectorIndex) {
        register int subsectorIndex;
        while (true) {
            let subsectorIndex = Random.randInt(144);
            if (~StarHackGame.occupied(sectorIndex, subsectorIndex)) {
                return subsectorIndex;
            }
        }
    }

    function boolean occupied(int sectorIndex, int subsectorIndex) {
        register int i;
        register Habitat habitat;
        register Anomaly anomaly;
        register Star star;
        register SectorScan sectorScan;
        let sectorScan = sectorScans.get(sectorIndex);
        if (sectorScan.stars() > 0) {
            let i = 0;
            while (i < 32) {
                let star = stars.get(i);
                if (star.sectorIndex() = sectorIndex) {
                    if (star.subsectorIndex() = subsectorIndex) {
                        return true;
                    }
                }
                inc i;
            }
        }
        if (sectorScan.habitats() > 0) {
            let i = 0;
            while (i < 8) {
                let habitat = habitats.get(i);
                if (habitat.sectorIndex() = sectorIndex) {
                    if (habitat.subsectorIndex() = subsectorIndex) {
                        return true;
                    }
                }
                inc i;
            }
        }
        if (sectorScan.anomalies() > 0) {
            let i = 0;
            while (i < 8) {
                let anomaly = anomalies.get(i);
                if (anomaly.sectorIndex() = sectorIndex) {
                    if (anomaly.subsectorIndex() = subsectorIndex) {
                        return true;
                    }
                }
                inc i;
            }
        }
        return false;
    }

    function Habitat findHabitat(int sectorIndex, int subsectorIndex, int d) {
        register int c, i;
        register Habitat habitat;
        let i = 0;
        while (i < 8) {
            let habitat = habitats.get(i);
            if ((~(habitat.isDestroyed())) & (habitat.sectorIndex() = sectorIndex)) {
                let c = StarMap.distance(
                    StarMap.subsectorX(subsectorIndex),
                    StarMap.subsectorY(subsectorIndex),
                    StarMap.subsectorX(habitat.subsectorIndex()),
                    StarMap.subsectorY(habitat.subsectorIndex()));
                if (c < d) {
                    return habitat;
                }
            }
            inc i;
        }
        return 0;
    }

    function SectorScan sectorScan(int sectorIndex) {
        return sectorScans.get(sectorIndex);
    }

    function PlayerShip playerShip() {
        return playerShip;
    }

    function boolean isRunning() {
        return (timeLeft > 0) & (anomaliesLeft > 0) & (~(playerShip.isDestroyed())) & (habitatsLeft > 0);
    }

    function void start() {
        // intro
        do CommandTerminal.awaitScreen(Constants.pressKey());
        do GameGraphics.wipeScreen();

        // main game loop 
        do Screens.gameScreen();
        do StarHackGame.drawSectorMap();
        do StarHackGame.drawGameStatus();
        
        return;
    }

    function void end() {
        do GameGraphics.wipeScreen();
        do StarHackGame.drawEndGameReport();
        do CommandTerminal.awaitScreen(Constants.pressKey());
        do GameGraphics.wipeScreen();
        return;
    }

    function void update() {
        // player's turn
        do CommandTerminal.clear();
        do StarHackGame.processCommand(CommandTerminal.readCommand(Constants.cmd()));

        // redraw
        do StarHackGame.drawSectorMap(playerShip);
        if (displayMode = 0) {
            do StarHackGame.drawGameStatus();
        }
        if (displayMode = 1) {
            do StarHackGame.drawStarMap();
        }
        if (displayMode = 2) {
            do StarHackGame.drawHelp();
            let displayMode = 0;
        }

        return;
    }

    function void clearStatusDisplay() {
        do TextGraphics.clearText(2, 29, 15, 64);
        return;
    }

    function void drawHelp() {
        register int i;
        do StarHackGame.clearStatusDisplay();
        do TextGraphics.drawTextAligned(Constants.help(), 2, 45, 1);
        do TextGraphics.drawTextAligned(Constants.divider(), 3, 45, 1);
        let i = 0;
        while (i < 9) {
            do TextGraphics.moveCursor(i + 5, 29);
            do TextGraphics.printString(CommandTerminal.commandHelp(i));
            inc i;
        }
        return;
    }

    function void drawGameStatus() {
        do StarHackGame.clearStatusDisplay();
        // game status
        do TextGraphics.drawTextAligned(Constants.missionStatus(), 3, 29, 0);
        do TextGraphics.drawTextAligned(Constants.divider(), 4, 29, 0);
        do TextGraphics.drawLabelValue(Constants.daysLeft(), (timeLeft / 100), 5, 29, 0);
        do TextGraphics.drawLabelValue(Constants.anomalies(), anomaliesLeft, 6, 29, 0);
        do TextGraphics.drawLabelValue(Constants.habitats(), habitatsLeft, 7, 29, 0);
        // ship status
        do TextGraphics.drawTextAligned(Constants.shipStatus(), 3, 44, 0);
        do TextGraphics.drawTextAligned(Constants.divider(), 4, 44, 0);
        do TextGraphics.drawLabelValue(Constants.weapons(), playerShip.weaponsStatus(), 5, 44, 0);
        do TextGraphics.drawLabelValue(Constants.engines(), playerShip.engineEfficiency(), 6, 44, 0);
        do TextGraphics.drawLabelValue(Constants.hull(), playerShip.hullIntegrity(), 7, 44, 0);
        do TextGraphics.drawLabelValue(Constants.power(), playerShip.powerReserve(), 8, 44, 0);
        if (playerShip.shieldsUp()) {
            do TextGraphics.drawLabelValue(Constants.shieldsStatUp(), playerShip.shieldLevel(), 9, 44, 0);
        } else {
            do TextGraphics.drawLabelValue(Constants.shieldsStatDown(), playerShip.shieldLevel(), 9, 44, 0);
        }
        do TextGraphics.drawLabelValue(Constants.missiles(), playerShip.missileSupply(), 10, 44, 0);
        do TextGraphics.drawLabelValue(Constants.drones(), playerShip.dronePopulation(), 11, 44, 0);
        return;
    }

    function void drawStarMap() {
        register int i, j, currentSectorIndex, sectorIndex;
        register SectorScan sectorScan;

        do StarHackGame.clearStatusDisplay();
        do TextGraphics.drawTextAligned(Constants.longRangeScan(), 2, 45, 1);
        do TextGraphics.drawTextAligned(Constants.divider(), 3, 45, 1);

        // header
        let i = 0;
        while (i < 8) {
            do TextGraphics.moveCursor(4, 32 + (i * 4));
            do TextGraphics.printInt(i);
            inc i;
        }

        // map
        let sectorIndex = 0;
        let currentSectorIndex = playerShip.sectorIndex();
        let i = 0;
        while (i < 8) {
            do TextGraphics.moveCursor(i + 5, 29);
            do TextGraphics.printInt(i);
            do TextGraphics.printChar(32);
            let j = 0;
            while (j < 8) {
                if (currentSectorIndex = sectorIndex) {
                    do TextGraphics.printChar(42);
                    do TextGraphics.printChar(42);
                    do TextGraphics.printChar(42);
                } else {
                    let sectorScan = sectorScans.get(sectorIndex);
                    if (sectorScan.scanned()) {
                        do TextGraphics.printInt(sectorScan.habitats());
                        do TextGraphics.printInt(sectorScan.stars());
                        do TextGraphics.printInt(sectorScan.anomalies());
                    } else {
                        do TextGraphics.printChar(0);
                        do TextGraphics.printChar(0);
                        do TextGraphics.printChar(0);
                    }
                }
                do TextGraphics.printChar(32);
                inc sectorIndex;
                inc j;
            }
            inc i;
        }

        return;
    }

    function void drawSectorMap() {

        register int i;
        register int count;
        register int sectorIndex;
        register int subsectorIndex;
        register Anomaly anomaly;
        register Habitat habitat;
        register Star star;
        register boolean hostilesPresent;
        
        let sectorIndex = playerShip.sectorIndex();
        let subsectorIndex = playerShip.subsectorIndex();
        
        do GameGraphics.clearTiles(2, 2, 14, 14);
        do Sprites.ship(2 + StarMap.subsectorX(subsectorIndex), 2 + StarMap.subsectorY(subsectorIndex));

        let i = 0;
        let count = anomalies.count();
        while (i < count) {
            let anomaly = anomalies.get(i);
            if (anomaly.sectorIndex() = sectorIndex) {
                let hostilesPresent = true;
                let subsectorIndex = anomaly.subsectorIndex();
                if (anomaly.isDestroyed()) {
                    do Sprites.debris(2 + StarMap.subsectorX(subsectorIndex), 2 + StarMap.subsectorY(subsectorIndex));
                } else {
                    do Sprites.anomaly(2 + StarMap.subsectorX(subsectorIndex), 2 + StarMap.subsectorY(subsectorIndex));
                }
            }
            inc i;
        }

        let i = 0;
        let count = habitats.count();
        while (i < count) {
            let habitat = habitats.get(i);
            if (habitat.sectorIndex() = sectorIndex) {
                let subsectorIndex = habitat.subsectorIndex();
                if (habitat.isDestroyed()) {
                    do Sprites.debris(2 + StarMap.subsectorX(subsectorIndex), 2 + StarMap.subsectorY(subsectorIndex));
                } else {
                    if (habitat.type() = 0) {
                        do Sprites.orbital(2 + StarMap.subsectorX(subsectorIndex), 2 + StarMap.subsectorY(subsectorIndex));
                    } else {
                        do Sprites.planet(2 + StarMap.subsectorX(subsectorIndex), 2 + StarMap.subsectorY(subsectorIndex));
                    }
                }
            }
            inc i;
        }
        
        let i = 0;
        let count = stars.count();
        while (i < count) {
            let star = stars.get(i);
            if (star.sectorIndex() = sectorIndex) {
                let subsectorIndex = star.subsectorIndex();
                do Sprites.star(2 + StarMap.subsectorX(subsectorIndex), 2 + StarMap.subsectorY(subsectorIndex));
            }
            inc i;
        }

        do TextGraphics.drawTextAligned(Constants.sectorName(playerShip.sectorIndex()), 21, 16, 1);
        if (hostilesPresent) {
            do TextGraphics.drawTextAligned(Constants.conditionRed(), 22, 16, 1);
        } else {
            do TextGraphics.drawTextAligned(Constants.conditionGreen(), 22, 16, 1);
        }
        return;        
    }

    function int endingState() {
        if (timeLeft < 1) {
            return 0;
        }
        if (habitatsLeft < 1) {
            return 1;
        }
        if (anomaliesLeft < 1) {
            return 2;
        }
        return 3;
    }   

    function void drawEndGameReport() {
        register int gameStatus;
        let gameStatus = StarHackGame.endingState();
        if (gameStatus = 0) {
            do TextGraphics.drawTextAligned(Constants.missionAborted(), 5, 32, 1);
            do TextGraphics.drawTextAligned(Constants.timeExpired(), 6, 32, 1);
            do TextGraphics.drawTextAligned(Constants.initiatingSpatialCollapse(), 7, 32, 1);
        }
        if (gameStatus = 1) {
            do TextGraphics.drawTextAligned(Constants.missionAborted(), 5, 32, 1);
            do TextGraphics.drawTextAligned(Constants.habitatsDestroyed(), 6, 32, 1);
            do TextGraphics.drawTextAligned(Constants.initiatingSpatialCollapse(), 7, 32, 1);
        }
        if (gameStatus = 2) {
            do TextGraphics.drawTextAligned(Constants.missionCompleted(), 5, 32, 1);
            do TextGraphics.drawTextAligned(Constants.anomaliesEliminated(), 6, 32, 1);
            if (friendlyFire) {
                do TextGraphics.drawTextAligned(Constants.friendlyFire(), 7, 32, 1);
                do TextGraphics.drawTextAligned(Constants.huntersDispatched(), 8, 32, 1);
            }
        }
        if (gameStatus = 3) {
            do TextGraphics.drawTextAligned(Constants.missionAborted(), 5, 32, 1);
            do TextGraphics.drawTextAligned(Constants.shipDestroyed(), 6, 32, 1);
        }

        do TextGraphics.drawTextAligned(Constants.divider(), 9, 32, 1);
        do TextGraphics.drawLabelValue(Constants.daysLeft(), (timeLeft / 100), 10, 20, 0);
        do TextGraphics.drawLabelValue(Constants.anomalies(), anomaliesLeft, 11, 20, 0);
        do TextGraphics.drawLabelValue(Constants.habitats(), habitatsLeft, 12, 20, 0);

        return;
    }

    function void processCommand(int commandCode) {
        register int x, y, xi, yj, i, j, d;
        register Anomaly anomaly;
        register Habitat habitat;
        register SectorScan sectorScan;
        // 0 -> LRS : Long Range Scan
        if (commandCode = 0) {
            let x = StarMap.sectorX(playerShip.sectorIndex());
            let y = StarMap.sectorY(playerShip.sectorIndex());
            let i = -1;
            while (i < 2) {
                let xi = x + i;
                if (~((xi < 0) | (xi > 7))) {
                    let j = -1;
                    while (j < 2) {
                        let yj = y + j;
                        if (~((yj < 0) | (yj > 7))) {
                            let sectorScan = sectorScans.get(StarMap.sectorIndex(xi, yj));
                            do sectorScan.setScanned(true);
                        }
                        inc j;
                    }
                }
                inc i;
            }
            let displayMode = 1;
            return;
        } 
        // 1 -> MJD : Micro Jump Drive in sector
        if (commandCode = 1) {
            do CommandTerminal.printMessage(Constants.microJumpTravel());
            let x = CommandTerminal.readDigit(Constants.coordX(), 0, 12);
            if (x < 0) {
                return;
            }
            let y = CommandTerminal.readDigit(Constants.coordY(), 0, 12);
            if (CommandTerminal.readYesNo(Constants.confirm())) {
                let d = playerShip.move(x, y);
                if (d = 0) {
                    do CommandTerminal.printMessage(Constants.noPower());
                    do CommandTerminal.readAnyKey(Constants.pressKey());
                } else {
                    do StarHackGame.useTime(d, false);
                }
            }
            let displayMode = 0;
            return;
        }
        // 2 -> JMP : JuMP to a nearby sector
        if (commandCode = 2) {
            do CommandTerminal.printMessage(Constants.jumpTravel());
            let x = CommandTerminal.readDigit(Constants.coordX(), 0, 8);
            if (x < 0) {
                return;
            }
            let y = CommandTerminal.readDigit(Constants.coordY(), 0, 8);
            if (y < 0) {
                return;
            }
            if (CommandTerminal.readYesNo(Constants.confirm())) {
                let d = playerShip.jump(x, y);
                if (d = 0) {
                    do CommandTerminal.printMessage(Constants.noPower());
                    do CommandTerminal.readAnyKey(Constants.pressKey());
                } else {
                    do StarHackGame.useTime(d, true);
                }
            }
            let displayMode = 0;
            return;
        }
        // 3 -> PAC : Fire Particle Cannon
        if (commandCode = 3) {
            do CommandTerminal.printMessage(Constants.fireLasers());
            let x = CommandTerminal.readDigit(Constants.coordX(), 0, 12);
            if (x < 0) {
                return;
            }
            let y = CommandTerminal.readDigit(Constants.coordY(), 0, 12);
            if (y < 0) {
                return;
            }
            if (CommandTerminal.readYesNo(Constants.confirm())) {
                let d = playerShip.fireLasers(x, y, 20);
                if (d > 0) {
                    do StarHackGame.playerFireUpon(x, y, d, true);
                    do StarHackGame.useTime(10, false);
                } else {
                    do CommandTerminal.printMessage(Constants.noPower());
                    do CommandTerminal.readAnyKey(Constants.pressKey());
                }
            }
            return;
        }
        // 4 -> AMM : Fire AntiMatter Missiles
        if (commandCode = 4) {
            do CommandTerminal.printMessage(Constants.fireMissiles());
            let x = CommandTerminal.readDigit(Constants.coordX(), 0, 12);
            if (x < 0) {
                return;
            }
            let y = CommandTerminal.readDigit(Constants.coordY(), 0, 12);
            if (y < 0) {
                return;
            }
            if (CommandTerminal.readYesNo(Constants.confirm())) {
                let d = playerShip.fireMissiles(x, y);
                if (d > 0) {
                    do StarHackGame.playerFireUpon(x, y, d, true);
                    do StarHackGame.useTime(10, false);
                } else {
                    do CommandTerminal.printMessage(Constants.noAmmo());
                }
            }
            return;
        }
        // 5 -> SDN : Shields DowN
        if (commandCode = 5) {
            do CommandTerminal.printMessage(Constants.shieldsDown());
            if (CommandTerminal.readYesNo(Constants.confirm())) {
                do playerShip.setShields(false);
            }
            return;
        }
        // 6 -> SUP : Shields UP
        if (commandCode = 6) {
            do CommandTerminal.printMessage(Constants.shieldsUp());
            if (CommandTerminal.readYesNo(Constants.confirm())) {
                do playerShip.setShields(true);
            }
            return;
        }
        // 7 -> RPR : RePaiR damage
        if (commandCode = 7) {
            do CommandTerminal.printMessage(Constants.repair());
            if (CommandTerminal.readYesNo(Constants.confirm())) {
                do playerShip.repair();
                do CommandTerminal.readAnyKey(Constants.pressKey());
                do StarHackGame.useTime(10, false);
            }
            return;
        }
        // 8 -> RSP : ReSuPply
        if (commandCode = 8) {
            do CommandTerminal.printMessage(Constants.resupply());
            if (CommandTerminal.readYesNo(Constants.confirm())) {
                if (StarHackGame.findHabitat(playerShip.sectorIndex(), playerShip.subsectorIndex(), 3) > 0) {
                    if (friendlyFire) {
                        do CommandTerminal.printMessage(Constants.resupplyRefuse());
                    } else {
                        do CommandTerminal.printMessage(Constants.resupplySuccess());
                        do playerShip.resupply();
                    }
                } else {
                    do CommandTerminal.printMessage(Constants.tooFar());
                }
                do CommandTerminal.readAnyKey(Constants.pressKey());
                do StarHackGame.useTime(10, false);
            }
            return;
        }
        // 9 -> STA : STAtus report
        if (commandCode = 9) {
            let displayMode = 0;
            return;
        }
        // cheat codes
        if (commandCode = 10) {
            if (CommandTerminal.readYesNo(Constants.confirm())) {
                do playerShip.hit(1000);
            }
            return;
        }
        if (commandCode = 11) {
            if (CommandTerminal.readYesNo(Constants.confirm())) {
                let i = 0;
                while (i < 8) {
                    let habitat = habitats.get(i);
                    if (~(habitat.isDestroyed())) {
                        do habitat.hit(1000);
                        if (habitat.isDestroyed()) {
                            let friendlyFire = true;
                            dec habitatsLeft;
                        }
                    }
                    inc i;
                }
            }
            return;
        }
        if (commandCode = 12) {
            if (CommandTerminal.readYesNo(Constants.confirm())) {
                let i = 0;
                while (i < 8) {
                    let anomaly = anomalies.get(i);
                    if (~(anomaly.isDestroyed())) {
                        do anomaly.hit(1000);
                        if (anomaly.isDestroyed()) {
                            dec anomaliesLeft;
                        }
                    }
                    inc i;
                }
            }
            return;
        }
        // else
        let displayMode = 2;
        return;
    }

    function void anomalyFireUpon(int sectorIndex, int subsectorIndex, int p, boolean animate) {
        register int i, x, y;
        register Habitat habitat;
        register SectorScan sectorScan;
        let x = StarMap.subsectorX(subsectorIndex);
        let y = StarMap.subsectorY(subsectorIndex);
        // animate strike
        if (animate) {
            do GameGraphics.animateTile(2 + x, 2 + y);
        }
        // process hit
        let i = 0;
        while (i < 8) {
            let habitat = habitats.get(i);
            if ((~(habitat.isDestroyed())) & (habitat.sectorIndex() = sectorIndex) & (habitat.subsectorIndex() = subsectorIndex)) {
                do habitat.hit(p);
                let sectorScan = sectorScans.get(sectorIndex);
                do sectorScan.setScanned(true);
                do CommandTerminal.clear();
                do CommandTerminal.printMessage(Constants.messageIncoming());
                do CommandTerminal.printMessage(Constants.sectorName(sectorIndex));
                if (habitat.isDestroyed()) {
                    dec habitatsLeft;
                    do sectorScan.incHabitats(-1);
                    do CommandTerminal.printMessage(Constants.habitatDestroyed());
                } else {
                    do CommandTerminal.printMessage(Constants.habitatDistress());
                }
                do CommandTerminal.readAnyKey(Constants.pressKey());
                return;
            }
            inc i;
        }
        if ((~(playerShip.isDestroyed())) & (playerShip.sectorIndex() = sectorIndex) & (playerShip.subsectorIndex() = subsectorIndex)) {
            do playerShip.hit(p);
            do CommandTerminal.clear();
            if (playerShip.isDestroyed()) {
                do CommandTerminal.printMessage(Constants.shipDestroyed());
            } else {
                do CommandTerminal.printMessage(Constants.shipHit());
            }
            do CommandTerminal.readAnyKey(Constants.pressKey());
        }
        return;
    }

    function void playerFireUpon(int x, int y, int p, boolean animate) {
        register int i, sectorIndex, subsectorIndex;
        register SectorScan sectorScan;
        register Habitat habitat;
        register Anomaly anomaly;
        do CommandTerminal.clear();
        // animate strike
        if (animate) {
            do GameGraphics.animateTile(2 + x, 2 + y);
        }
        // process hit
        let sectorIndex = playerShip.sectorIndex();
        let subsectorIndex = StarMap.subsectorIndex(x, y);
        let i = 0;
        while (i < 8) {
            let habitat = habitats.get(i);
            if ((~(habitat.isDestroyed())) & (habitat.sectorIndex() = sectorIndex) & (habitat.subsectorIndex() = subsectorIndex)) {
                do habitat.hit(p);
                if (habitat.isDestroyed()) {
                    dec habitatsLeft;
                    let friendlyFire = true;
                    let sectorScan = sectorScans.get(sectorIndex);
                    do sectorScan.incHabitats(-1);
                    do CommandTerminal.printMessage(Constants.habitatDestroyed());
                } else {
                    do CommandTerminal.printMessage(Constants.friendlyFire());
                }
                do CommandTerminal.readAnyKey(Constants.pressKey());
                return;
            }
            inc i;

        }
        let i = 0;
        while (i < 8) {
            let anomaly = anomalies.get(i);
            if ((~(anomaly.isDestroyed())) & (anomaly.sectorIndex() = sectorIndex) & (anomaly.subsectorIndex() = subsectorIndex)) {
                do anomaly.hit(p);
                if (anomaly.isDestroyed()) {
                    dec anomaliesLeft;
                    let sectorScan = sectorScans.get(sectorIndex);
                    do sectorScan.incAnomalies(-1);
                    do CommandTerminal.printMessage(Constants.anomalyDestroyed());
                } else {
                    do CommandTerminal.printMessage(Constants.anomalyHit());
                }
                do CommandTerminal.readAnyKey(Constants.pressKey());
                return;
            }
            inc i;
        }
        return;
    }

    function void useTime(int t, boolean inJump) {
        register int i;
        register Anomaly anomaly;
        let timeLeft = timeLeft - t;
        if (~inJump) {
            let i = 0;
            while (i < 8) {
                let anomaly = anomalies.get(i);
                if (~(anomaly.isDestroyed())) {
                    do anomaly.update();
                }
                inc i;
            }
            do playerShip.update();
        }
        return;
    }

}